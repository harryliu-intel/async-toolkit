#!/usr/bin/env perl

use strict;
use warnings;

use File::Spec::Functions qw(catdir);
use File::Basename qw(dirname);
use English qw(-no_match_vars);
use Cwd qw(abs_path);
use Term::ANSIColor;
use FileHandle;
use Readonly;
use Expect;
use Carp;


Readonly my $MAX_PORT => 22;
Readonly my $TIMEOUT => 8;
Readonly my $EOT => -1;

# Evaluate all paths and run the application
my @params = qw(-m ../../../../main/m3/model_server/AMD64_LINUX/models.packetServer);
@params = @ARGV if @ARGV;
my $path = catdir($ENV{'MODEL_ROOT'}, 'wm/src/client/c/examples/client_regwrite');
$path = catdir(dirname(dirname(dirname(abs_path($PROGRAM_NAME)))), 'examples/client_regwrite') if not -e $path;
my $reg_file = catdir(dirname($path), 'fpps_reg_table.c');
my $client = do {
	local $ENV{'LD_LIBRARY_PATH'} = '../..';
	Expect->spawn($path, @params) or die colored("Could not start application $path\n", 'bold red')
};

# Wait for application prompt: <0>%
sub expect_prompt {
	$client->expect($TIMEOUT, '<0>% ') or carp colored("Prompt not occured\n", 'yellow');
	return;
}

# Write the value to the register
sub write_value {
	my ($reg, $val) = @_;
	print {$client} "write $reg $val\n";
	expect_prompt;
	return;
}

# Read the value from register and compare with the expected one
sub read_value {
	my ($reg, $val) = @_;

	print {$client} "read $reg\n";
	$client->expect($TIMEOUT, '-re', '\-\> (0x[a-f0-9]+)\s') or croak colored("Could not read from register $reg\n", 'bold red');
	expect_prompt;

	my $output = hex(($client->matchlist())[0]);
	($output == $val) or croak colored("Expected $val, but got $output\n", 'bold red');

	print colored('Read value correct', 'green');
	print {$client} "\n";
	expect_prompt;

	return;
}

# Send a packet to WM
sub send_packet {
	my ($port, @packet) = @_;
	print {$client} "send $port @packet\n";
	expect_prompt;
	return;
}

# Receive a packet from WM (port -1 if EOT packet expected), no argument if no validation expected
sub receive_packet {
	my ($port, @packet) = @_;

	print {$client} "receive\n";

	if (!defined $port || $port >= 0) {
		# Receive regular packet
		$client->expect($TIMEOUT, '-re', 'Received (\d+) .* port (\d+)') or croak colored("Could not receive a frame\n", 'bold red');
	}
	else {
		# Receive EOT packet
		$client->expect($TIMEOUT, 'EOT packet received') or carp colored("EOT packet expected\n", 'yellow');
	}

	# Arguments provided, validate the packet
	if (defined $port && $port >=0) {
		my $rx_port = int(($client->matchlist())[1]);
		($rx_port == $port) or croak colored("Packet received on port $rx_port, but expected on $port\n", 'bold red');
	}

	if (@packet) {
		my $bytes = int(($client->matchlist())[0]);
		($bytes == scalar @packet) or croak colored("Expected packet size ${\scalar @packet} but received $bytes bytes\n", 'bold red');
	}

	expect_prompt;
	print colored('Received packet correct', 'green');
	print {$client} "\n";
	expect_prompt;

	return;
}

sub get_reg_list {
	my ($reg_table) = @_;
	my @registers;

	open my $fh, '<', $reg_table or croak colored("Could not open register table: $reg_table", 'bold red');

	while (<$fh>) {
		if (/\s[{]\"(\w+)\".*[}]/xms) {
			push @registers, $1;
		}
	}

	close $fh or croak colored("Could not close register table: $reg_table", 'bold red');
	return @registers;
}

# Test variables
my @packet = qw(
    0x00 0x01 0x02 0x03 0x04 0x05 0x06 0x07 0x08 0x09 0x0a 0x0b
    0x0c 0x0d 0x0e 0x0f 0x10 0x11 0x12 0x13 0x14 0x15 0x16 0x17
    0x18 0x19 0x1a 0x1b 0x1c 0x1d 0x1e 0x1f 0x20 0x21 0x22 0x23
    0x24 0x25 0x26 0x27 0x28 0x29 0x2a 0x2b 0x2c 0x2d 0x2e 0x2f
    0x30 0x31 0x32 0x33 0x34 0x35 0x36 0x37 0x38 0x39 0x3a 0x3b
    0xee 0x7f 0xec 0xb0
    );
my @registers = get_reg_list($reg_file);
my $value = 0xBEEF;

# The application is running, perform tests

# Make sure, the model is connected
$client->expect($TIMEOUT, 'Started/connected') or warn colored("No response from white model, trying to perform test anyway...\n", 'yellow');

# Write and read from each register
foreach my $reg (@registers) {
	write_value($reg, $value);
	read_value($reg, $value);
	++$value;

	print {$client} "\n";
	expect_prompt;
}

# Send and receive a packet to some ports, then wait for EOT packet
foreach my $port (0..$MAX_PORT) {
	send_packet($port, @packet);
	receive_packet($port, @packet);
	receive_packet($EOT);

	print {$client} "\n";
	expect_prompt;
}

# Disconnect
print {$client} "quit\n";
$client->expect($TIMEOUT, 'Disconnected') or warn colored("Disconnection failed\n", 'yellow');

