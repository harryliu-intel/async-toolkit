
/* generic template generator. */
/* commands: gmodule, ginterface, gimplementation. */
/* other commands: build_generic_module. */

/* $Id$ */


_M3TMPLHACK = TOP & "/m3tmplhack/" & BUILD_DIR & "/m3tmplhack"

readonly proc Build_generic_module(nam, gen, args1, args2) is
        build_generic_intf(nam, gen, args1, VISIBLE)
        build_generic_impl(nam, gen, args2)
end

readonly proc build_generic_module(nam, gen, args1, args2) is
        build_generic_intf(nam, gen, args1, HIDDEN)
        build_generic_impl(nam, gen, args2)
end

readonly proc _exec(cmd) is
        local ret = exec(cmd)
        write (cmd, CR)
        if not equal(ret, 0) error("command failed with error code: ", ret) end
end

readonly proc derived_template(nm) is
	template("../" & BUILD_DIR & "/" & nm)
end

readonly proc _check_clean(g) is
	if defined("_clean")
		write("delete " & g, CR)
		/* deleting this file causes staleness, 
			which is desired effect of clean. */
		delete_file(g)
	end
end

/*
assumption: templates need only be generated when m3makefile changes.
extra cleaning required if these commands are used 
in a template imported by m3makefile!
*/

readonly proc gmodule_template(nm, intf_args, impl_args) is
	local source = path_of("m3makefile")
	local f = nm & ".tmpl"
	local g = nm & ".generate_tmpl"
	if stale(f, source) or stale(g, source)
		> g in
			write("module", CR)
			write(nm, CR)
			write(intf_args, CR)
			write(impl_args, CR)
		end
		_exec([_M3TMPLHACK,g])
	end
	_check_clean(g)
	derived_template(nm)
end

readonly proc gother_template(nm, args, kind) is
	local source = path_of("m3makefile")
	local base = nm & "_" & kind
	local f = base & ".tmpl"
	local g = base & ".generate_tmpl"
	if stale(f, source) or stale(g, source)
		> g in
			write(kind, CR)
			write(nm, CR)
			write(args, CR)
		end
		_exec([_M3TMPLHACK,g])
	end
	_check_clean(g)
	derived_template(base)
end

readonly proc gmodule(nm, intf_args, impl_args) is
	generic_module(nm)
	gmodule_template(nm, intf_args, impl_args)
end

readonly proc Gmodule(nm, intf_args, impl_args) is
	Generic_module(nm)
	gmodule_template(nm, intf_args, impl_args)
end

readonly proc ginterface(nm, args) is
	generic_interface(nm)
	gother_template(nm, args, "interface")
end

readonly proc Ginterface(nm, args) is
	Generic_interface(nm)
	gother_template(nm, args, "interface")
end

readonly proc gimplementation(nm, args) is
	generic_implementation(nm)
	gother_template(nm, args, "implementation")
end

readonly proc Gimplementation(nm, args) is
	Generic_implementation(nm)
	gother_template(nm, args, "implementation")
end
